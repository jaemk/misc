(defpackage advent19.d08
  (:use :cl :arrow-macros :metabang-bind)
  (:export
    :input
    :parse
    :part-1
    :part-2
    :run))
(in-package advent19.d08)
(named-readtables:in-readtable :interpol-syntax)

(defun parse (in &key (size '(25 6)))
  (bind ((in (map 'list #'digit-char-p in))
         (total-size (length in))
         (layer-size (apply #'* size)))
    (loop for start from 0 to total-size by layer-size
          for end = (+ start layer-size)
          when (>= total-size end)
            collect (subseq in start end))))

(defun input ()
  (->>
    (str:from-file "../input/d08.txt")
    (str:trim)
    (parse)))

(defun part-1 (layers)
  (bind ((fewest-zero-count nil)
         (ones-twos nil))
    (loop for layer in layers
          do (bind ((zero-count 0)
                    (ones-count 0)
                    (twos-count 0))
               (loop for c in layer
                     when (= c 0)
                       do (incf zero-count)
                     when (= c 1)
                       do (incf ones-count)
                     when (= c 2)
                       do (incf twos-count))
               (when (or (null fewest-zero-count) (> fewest-zero-count zero-count))
                 (setf fewest-zero-count zero-count)
                 (setf ones-twos (* ones-count twos-count)))))
    ones-twos))

(defun format-layer (layer &key (size '(25 6)))
  (bind ((layer (mapcar (lambda (n) (if (zerop n) " " "X")) layer))
         (len (length layer))
         ((w h) size))
    (->>
      (loop for start from 0 to len by w
            for end = (+ start w)
            when (>= len end)
              collect (format nil "狺篚怏羼灬弪篝狎孱洎┅篝蚝觑轭？④睥┅┅ㄤ彐躅疳螋灬弪脲ㄤ轶痨狴Ж驳订┅ㄢ轭è骈钺ㄦ轵篝灬弪螬灬弪蝈篝灬弪螬ㄤ镱铋飑祜镳骘灬弪轭灬弪麒孱铒滹铄滹ㄢ轭è蝈磲轭轭癌磲痨灬礅溽ㄦ轭灬麒孱ㄦ轵篝骈瞟ㄩ铒ㄦ轵篝灬┅箦翩ㄦ轵篝骈瞟ㄦ轵篝灬┅ㄩ钽蝈磲轭轭绌┅骈钺灬弪麒孱弪镳蝈磲轭轭绌箦翩滹铄舂┅ㄩ溟箴灬ō揪ㄦ矧磲舡灬弪骈钺后辁溟箴灬ㄦ矧磲铋幄┅骈钺飑┅ㄤ彐躅蝓ī戾è轭ㄩ铕豸┅ㄢ轭èê鲠祯弩蝈眢ㄡ漩孱舯巩豸殪蠛鏖翳糸黹铉疳螋轭┅┅ㄦ矧磲π狎犴螬幄眢蝈螬ㄢ轭èê鲠祯弩蝈眢ㄡ漩孱舯巩豸殪蠛鏖翳糸黹铉疳螋轭┅┅ㄦ矧磲π狎犴螬幄眢蝈螬┅